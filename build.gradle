plugins {
    id 'idea'
    id 'java'
    id "io.github.patrick.remapper" version "1.4.0"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'io.ebean' version '13.17.4'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    maven { url "https://reposilite.appleptr16.com/appleptr16" }
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    mavenCentral()
    maven {
        name 'CodeMC'
        url 'https://repo.codemc.org/repository/maven-public/'
    }
    maven {
        name 'sk89q-repo'
        url 'https://maven.enginehub.org/repo/'
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(18))
    }
}

project.group groupValue
project.version versionValue

publishing {
    repositories {
        maven {
            name 'appleptr16Repo'
            url 'https://reposilite.appleptr16.com/appleptr16'
            credentials(PasswordCredentials)
        }
    }
    publications {
        mavenJava(MavenPublication) {
            setGroup groupValue
            setVersion versionValue
            artifact(jar)
        }
    }
}

configurations {
    configureEach {
        resolutionStrategy.cacheDynamicVersionsFor 1, 'seconds'
        resolutionStrategy.cacheChangingModulesFor 1, 'seconds'
    }
}
configurations {
    testImplementation.extendsFrom compileOnly
}

project.ext.ebean_version = '13.17.4'
// https://search.maven.org/search?q=g:io.ebean
dependencies {
    // sql
    annotationProcessor "io.ebean:ebean-querybean:${ebean_version}"
    annotationProcessor "io.ebean:querybean-generator:${ebean_version}"
    compileOnly "io.ebean:ebean-querybean:${ebean_version}"
    compileOnly "io.ebean:ebean-mysql:${ebean_version}"
    // sql driver

    // migration
    compileOnly "io.ebean:ebean-ddl-generator:${ebean_version}"
    compileOnly 'io.ebean:ebean-migration:13.7.0'
    compileOnly "org.glassfish.jaxb:jaxb-runtime:2.3.2"

    //json
    compileOnly "io.ebean:ebean-jackson-mapper:${ebean_version}"
    compileOnly "io.ebean:ebean-jackson-jsonnode:${ebean_version}"
    compileOnly 'com.fasterxml.jackson.core:jackson-annotations:2.14.2'
    compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.14.2'

    // driver
    compileOnly 'mysql:mysql-connector-java:8.0.33'

    // logging
    compileOnly 'org.apache.logging.log4j:log4j-core:2.20.0'
    compileOnly 'org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0'

    // volt
    compileOnly 'mc.pmc:voltpluginmanager:3.2-SNAPSHOT'
    compileOnly 'mc.apple:AppleMCUtil:1.0-SNAPSHOT'
    compileOnly 'mc.nms:DecodingNms:1.20.1-2.0-SNAPSHOT'
    compileOnly 'voltskiya.utilities:VoltskiyaUtilities:2.0-SNAPSHOT'
    compileOnly 'voltskiya.mob:VoltskiyaMobManager:2.0-SNAPSHOT'
    compileOnly 'voltskiya.lib:LoadDependencies:1.0-SNAPSHOT'

    // web
    compileOnly 'voltskiya.web:VoltskiyaWebserver:1.0-SNAPSHOT'
    compileOnly 'io.javalin:javalin:5.2.0'

    // minecraft api
    compileOnly 'org.spigotmc:spigot:1.20.1-R0.1-SNAPSHOT:remapped-mojang'
    compileOnly 'io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT'

    // minecraft libraries
    compileOnly 'de.tr7zw:item-nbt-api-plugin:2.11.2'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.8'
}

ebean {
    debugLevel = 0 //0 - 9
}
shadowJar.classifier ''

remap {
    dependsOn(shadowJar)
    version.set '1.19.4'
    doLast {
        copy {
            from jar.getArchiveFile()
            into file(serverBuildDir)
        }
    }
}
shadowJar.classifier ''
shadowJar {
    manifest {
        attributes 'Multi-Release': true
    }
    mergeServiceFiles()
    relocate 'io.ebean', 'com.voltskiya.io.ebean'
    relocate 'io.ebeaninternal', 'com.voltskiya.io.ebean'
    relocate 'io.ebeanservice', 'com.voltskiya.io.ebean'
    outputs.upToDateWhen { false }
}

publish {
    dependsOn(jar)
}